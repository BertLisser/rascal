Name: String

Synopsis: String values.

Syntax:
`"$StringChar_1$$StringChar_2$..."`
where $StringChar$ may be one of the following:
* __Ordinary character__: Any character except `<`, `>`, `"`, `'` or `\`.
* __Escaped character__: Backslash `\` followed by any of  `<`, `>`, `"`, `'` or `\` represents the escaped character itself.
  Other escape sequences that are supported are:
** `\n`: newline
** `\t`: tab
** `\r`: carriage return
** `\b`: backspace
** `\f`: vertical feed
** `\u$hexDigit_1$$hexDigit_2$$hexDigit_3$$hexDigit_4$` : hexadecimal escapes with four digit indexes into UNICODE.
** `\U$hexDigit_1$$hexDigit_2$$hexDigit_3$$hexDigit_4$$hexDigit_5$$hexDigit_6$` : hexadecimal escapes with six digit indexes into UNICODE.
** `\a$hexDigit_1$$hexDigit_2$`:  hexadecimal escapes with 2 digit indexes into ASCII (0x0 ... 0x7F).


* __String Interpolation__: 

| Form      | Description |
+-----------+-------------|
| `<$Exp$>` | Interpolate the value of the expression as a string
| `<if($Exp$){> ... $StringChars$ ... <}>` | Conditional inclusion of $Text$, where $StringChars$ may use variables introduced in $Exp$ |
| `<if($Exp$){> ... $StringChars_1$ ... <} else {>  ... $StringChars_2$ ... <}>` | Conditional inclusion of either $StringChars_1$ or $StringChars_2$ |
| `<for($Exp$){>... $StringChars$ ... <}>` | Iterative splicing of $StringChars$ into the result, where $StringChars$ may use variables introduced in $Exp$.|
| `<while($Exp$){> ... $StringChars$ ... <}>` | Iterative splicing of $StringChars$ into the result, where $StringChars$ may use variables introduced in $Exp$.|
| `<do {>... $StringChars$ ... <} while ($Exp$)>` | Iterative splicing of $StringChars$ into the result, where $StringChars$ may use variables introduced in $Exp$.|

* __Multiline__:
| Form | Description |
+------+-------------+
| `$StringChars_1$\n $StringChars_2$ ` | Strings can be multi-line without an escape or continuation marker |
| `$StringChars_2$\n '  $StringChars_2$` | A margin character `'` indicates where the next line starts |


Usage: `import String`; (included in [Prelude])

Types:
`str`

Function:

Details:

Description:

The string values are represented by the type `str` and consist of character 
sequences surrounded by double quotes, e.g., `"a"` or `"a\nlong\nstring"`.

_Multiline_: Strings may span more than one line. The margin character `'` indicates which part of a line will be ignored. This is useful for indenting a multi-line string with the source code that generates it.

_Interpolation_:  String literals support so-called _string interpolation_: 
inside string constants text between angle brackets (`<` and `>`) is first executed and then replaced by
 its string value.
Various statements (if, for, while, do) also return a value and can be used in this way.
In the interpolation variant of these statements the block or blocks that are part of the statement become arbitrary text 
(that may itself contain interpolations). 

_Auto-indent_: Expressions that get interpolated in a string will be auto-indented. This means that each line that results from the evaluation of the expression is prefixed with the indentation level of the position of the expression in the current string.

The following functions are provided for strings:
<toc Rascal/Expressions/Values/String 1>

Examples:
<screen>
N = 13;
"The value of N is <N>";
"The value of N*N is <N*N>";
"The value is <(N < 10) ? 10 : N*N>";
//As you can see the string value of variables and expressions is interpolated in the result as expected. 
//<br>
//Some examples of more advances string interpolation 
"N is <if(N < 10){> small <} else {> large <}>";
"N is <if(N < 10){> small <} else {> large (<N>)<}>";
"before <for(x<-[1..5]){>a <x> b <}>after";
// multi-line string
import IO;
println("hello
this
  is
    new")
// multi-line string with margin:
if (true)
  println("this is
          'what
          '  margins
          'are good for
          ");
// auto indent:
str genMethod(str n) = "int <n>() {
                       '  return 0;
                       '}";
str genClass() = "class myClass {
                 '  <genMethod("myMethod")>
                 '}";
println(genClass());
</screen>


Benefits:
String interpolation enables very flexible template-based text generation as used in generators for
source code, markup and the like.

Pitfalls:

Questions:

